define min(x, y) {
	if (x < y) { return(x) }
	return(y)
}

define abs(x) { return(-min(x, -x)) }

define pi() { return(4*a(1)) }

define arcsin(x) { return(a(x / sqrt(1 - (x^2)))) }

define deg2rad(deg) { return(deg*pi()/180) }

# Returns the distance, in meters, between two decimal latitude/longitude
# coordinates (in degrees).
#
# Implements the formula discribed at:
#   http://www.movable-type.co.uk/scripts/latlong.html
define lnglat_distance(lng1_deg, lat1_deg, lng2_deg, lat2_deg) {
	lng1 = deg2rad(lng1_deg)
	lat1 = deg2rad(lat1_deg)
	lng2 = deg2rad(lng2_deg)
	lat2 = deg2rad(lat2_deg)
	r = 6371000

	delta_lat = abs(lat1 - lat2)
	delta_lng = abs(lng1 - lng2)
	a = (s(delta_lat / 2) ^ 2) + c(lat1) * c(lat2) * (s(delta_lng / 2) ^ 2)
	c = 2 * arcsin(min(1, sqrt(a)))
	d = r * c
	return(d)
}
